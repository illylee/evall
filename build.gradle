buildscript {
    ext {
        springBootVersion = '2.1.8.RELEASE'
        targetDir = 'target'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"

    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.evall'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

// jar 플젝 구성
configure(subprojects.findAll {it.name == 'evall-common'} ) {
    jar {
        enabled = true
    }

    bootJar {
        enabled = false
    }

}

// war 플젝 구성
configure(subprojects.findAll {it.name == 'evall-api' || it.name == 'evall-admin' || it.name == 'evall-pweb'}) {
    apply plugin: 'war'
    war {
        baseName = project.name
        destinationDir = file("$targetDir")
    }

    configurations {
        customProvidedRuntime
    }
    dependencies {
        customProvidedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    }
}

project(':evall-common') {
    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        runtime('com.h2database:h2')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

project(':evall-api') {
    dependencies {
        compile project(':evall-common')

    }
}

project(':evall-pweb') {
    dependencies {
        compile project(':evall-common')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        compile('org.springframework.boot:spring-boot-devtools')
        compile('org.apache.tomcat.embed:tomcat-embed-jasper')
        compile('javax.servlet:jstl:1.2')
    }
}

project(':evall-admin') {
    dependencies {
        compile project(':evall-common')

    }
}